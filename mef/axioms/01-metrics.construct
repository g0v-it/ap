#
# (c)2019 Enrico Fagnoni (enrico at LinkedData.Center) with CC-BY license
#
##############################################################################
##  The value of the component metrics is computed as the sum of the  facts
##############################################################################
PREFIX fr: <http://linkeddata.center/botk-fr/v1#>
PREFIX mef: <http://w3id.org/g0v/it/mef#>

CONSTRUCT { 
	?component 
		mef:competenza ?competenzaSum ;
		mef:cassa ?cassaSum ;
		mef:residui ?residuiSum
}
WHERE {
	SELECT ?component (SUM(?competenza) AS ?competenzaSum) (SUM(?cassa) AS ?cassaSum) (SUM(?residui) AS ?residuiSum) WHERE {
		?fact a mef:Fact ;  
			fr:isPartOf+ ?component  ;
			mef:competenza ?competenza;
			mef:cassa ?cassa ;
			mef:residui ?residui; 
	} GROUP BY ?component
}	