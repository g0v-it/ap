#
# (c)2019 Enrico Fagnoni (enrico at LinkedData.Center) with CC-BY license
#
##############################################################################
##  all component are related to one or more taxonomies exposed by budget
##############################################################################
PREFIX mef: <http://w3id.org/g0v/it/mef#>

CONSTRUCT { ?structuralComponent mef:inTaxonomy ?taxonomyScheme }
WHERE  { 
	{		
		?structuralComponent a ?componentType; mef:inBudget/mef:hasSchemeSMMPACP ?taxonomyScheme .
		VALUES ?componentType {
			mef:Spesa
			mef:Ministero
			mef:MissioneMinistero
			mef:Programma
			mef:Azione
			mef:CapitoloDiSpesa
			mef:PianoDiGestione
		}
	} 
	UNION
	{
		?structuralComponent a ?componentType; mef:inBudget/mef:hasSchemeSMRMP ?taxonomyScheme .
		VALUES ?componentType {
			mef:Spesa
			mef:Ministero
			mef:CentroResponsabilita
			mef:MissioneMinistero
			mef:Programma
		}
	} 
	UNION
	{
		?structuralComponent a ?componentType; mef:inBudget/mef:hasSchemeSMTC ?taxonomyScheme  .
		VALUES ?componentType {
			mef:Spesa
			mef:TitoloSpesa
			mef:CategoriaSpesa
		}
	} 
	UNION
	{
		?structuralComponent a ?componentType; mef:inBudget/mef:hasSchemeSTCCP ?taxonomyScheme  .
		VALUES ?componentType {
			mef:Spesa
			mef:TitoloSpesa
			mef:CategoriaSpesa
			mef:CapitoloDiSpesa
			mef:PianoDiGestione
		}		
	} 
	UNION
	{
		?structuralComponent a ?componentType; mef:inBudget/mef:hasSchemeSMP ?taxonomyScheme  .
		VALUES ?componentType {
			mef:Spesa
			mef:Missione
			mef:Programma
		}		
	} 
	UNION
	{
		?structuralComponent a ?componentType; mef:inBudget/mef:hasSchemeETNT ?taxonomyScheme  .
		VALUES ?componentType {
			mef:Entrata
			mef:TitoloEntrata
			mef:Natura
			mef:Tipologia
		}
	} 
	UNION
	{
		?structuralComponent a ?componentType; mef:inBudget/mef:hasSchemeETTPCA ?taxonomyScheme  .
		VALUES ?componentType {
			mef:Entrata
			mef:TitoloEntrata
			mef:Tipologia
			mef:Provento
			mef:CapitoloEntrata
			mef:Articolo
		}		
	} 
}