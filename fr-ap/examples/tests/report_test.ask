PREFIX fr: <http://linkeddata.center/botk-fr/v1#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX sdmx-attribute:	<http://purl.org/linked-data/sdmx/2009/attribute#> 

ASK {
	FILTER( ?completeReports = ?allReports )
	
	# Count complete reports
	{
		SELECT (COUNT(DISTINCT ?ds) AS ?completeReports)
		WHERE {
		    ?ds a fr:FinancialReport, qb:DataSet, skos:ConceptScheme .
		
		    # Every financial report has exactly one associated valid reference period.   
		    {
		        ?ds fr:refPeriod ?refPeriod1.
		        ?refPeriod1 time:hasBeginning ?begins; time:hasEnd ?ends
		        FILTER( ?begins != ?ends )
		        FILTER NOT EXISTS { ?obs fr:refPeriod ?refPeriod2 FILTER (?refPeriod1 != ?refPeriod2) }
		    }
		    
		 
		    # Every financial report exactly  unitMeasure attribute .    
		    {
		        ?ds sdmx-attribute:unitMeasure ?unit1 .
		        ?unit1 skos:inScheme <http://publications.europa.eu/resource/authority/currency>;
		            skos:prefLabel ?label
		        FILTER NOT EXISTS { ?ds sdmx-attribute:unitMeasure ?unit2 FILTER (?unit1 != ?unit2) }
		    }
		}
    }
    
 	# Count all reports
	{
		SELECT (COUNT(DISTINCT ?ds) AS ?allReports)
		WHERE {
		    ?ds a fr:FinancialReport
		}
    }
}
    